#!groovy

pipeline {
    agent any

    parameters {
        choice(name: 'sourceEnvironment', choices: ['dev', 'test', 'prod'], description: 'The environment to deploy the image from')
        choice(name: 'targetEnvironment', choices: ['dev', 'test', 'prod'], description: 'The environment to deploy to')
        string(name: 'version', defaultValue: 'latest', description: 'The image version to release', trim: true)
    }

    environment {
        MEGA_PROJECT = "57-mega-"
        SOURCE_PROJECT = "${env.MEGA_PROJECT + params.sourceEnvironment}"
        TARGET_PROJECT = "${env.MEGA_PROJECT + params.targetEnvironment}"
        OCP_ROUTE_SUFFIX = "${env.TARGET_PROJECT}.cloud.itandtel.at"
    }

    options {
        disableConcurrentBuilds()
    }

    stages {
        stage('Prepare') {
            steps {
                script {
                    echo "\
Source-Project: ${env.SOURCE_PROJECT} \n\
Target-Project: ${env.TARGET_PROJECT} \n\
Image-Tag:      ${params.version}\
"
                    sh "oc project ${env.TARGET_PROJECT}"
                }
            }
        }

        stage('Purge') {
            steps {
                script {
                    dir('apps/mega-zep') {
                        openshift.withCluster() {
                            deleteServiceResources("mega-zep-backend", params.targetEnvironment)
                            deleteServiceResources("mega-zep-frontend", params.targetEnvironment)
                        }
                    }
                }
            }
        }

        stage('Tag-Image') {
            when {
                expression { !params.environment.equals("dev") }
            }
            steps {
                script {
                    openshift.withCluster() {
                        openshift.tag("${env.SOURCE_PROJECT}/mega-zep-frontend:${params.version}", "${env.TARGET_PROJECT}/mega-zep-frontend:${params.version}")
                        openshift.tag("${env.SOURCE_PROJECT}/mega-zep-backend:${params.version}", "${env.TARGET_PROJECT}/mega-zep-backend:${params.version}")
                    }
                }
            }
        }

        stage("Release") {
            parallel {
                stage("Fronted") {
                    steps {
                        script {
                            dir('apps/mega-zep') {
                                openshift.withCluster() {
                                    createServiceResources("mega-zep-frontend", params.targetEnvironment)
                                }
                            }
                        }
                    }
                }

                stage("Backend") {
                    steps {
                        script {
                            dir('apps/mega-zep') {
                                openshift.withCluster() {
                                    def frontendProps = readServiceProperties("mega-zep-frontend", ".", params.targetEnvironment)
                                    def service = "https://${frontendProps.NAME}-${env.OCP_ROUTE_SUFFIX}"
                                    createServiceResources("mega-zep-backend",
                                            params.targetEnvironment,
                                            "--param=JAVA_OPTIONS='-Djava.net.preferIPv4Stack=true -Dquarkus.http.cors.origins=${service}' \
                                            --param=IMAGE_STREAM_TAG='${params.version}'")
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

def deleteServiceResources(String service, String stage = "") {
    if (stage != "") {
        stage = "." + stage
    }
    sh "oc process --filename ${service}.yaml \
                   --param-file=${service}${stage}.properties \
                   | oc delete --ignore-not-found --filename -"
}

def createServiceResources(String service, String stage = "", String additionalParams = '') {
    if (stage != "") {
        stage = "." + stage
    }
    sh "oc process --filename ${service}.yaml \
                   --param-file=${service}${stage}.properties ${additionalParams} \
                   | oc create --filename -"
}

def readServiceProperties(String service, String path = ".", String stage = "") {
    if (stage != "") {
        stage = "." + stage
    }
    def props = readProperties file: "${path}/${service}${stage}.properties"
    return props
}

